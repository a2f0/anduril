### $Id: grep.stubs,v 1.5 2001/06/25 22:56:54 wd Exp $
ac.package grep

alias grep {
	if (![$0]) {
		@usage(grep)
		return
	}
	
	^local i,x,re,exp,s.,l.,win
	@:win=[1]
	
	if ([$0]==[-w]) {
		if (![$2]) {
			return
		}
		@:win=[$1]
		
		@:exp=[$2-]
	}{
		@:exp=[$0-]
	}
	
	if (win==[*]) {
		fe ($winrefs()) xx {
			if (winlevel($xx)) {
				grep -w $xx $exp
			}
		}
		return
	}
	
	if (!winlevel($win)) {
		@:win=[]
	}
	
	@:re=regcomp($exp)
	
	### grep here, and save the lines
	@:i=LASTLOG
	@:x=0
	while (i) {
		if (!regexec($re $line($i $win))) {
			@:s[$x]=line($i $win)
			@:l[$x]=i
			@:x++
		}
		@:i--
	}
	
	if (!x) {
		aerrecho no matches found for expression $exp
	} else {
		@:winlev = getset(LASTLOG_LEVEL)
		^window lastlog_level none
		a.box.echocmd xecho -nolog
		a.box.start grep '${exp}'
		@:i=0
		while (i<x) {
			a.box.echo [$[4]l[$i]] $s[$i]
			@:i++
		}
		a.box.end
		^window lastlog_level $winlev
	}
	@regfree($re)
}

# vim: syntax=off ts=3 ai smartindent
